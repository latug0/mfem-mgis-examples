@Behaviour Elasticity;
@Author Helfer Thomas;
@Date 11 / 12 / 06;
@Description{
  A very first test (the most simple one in fact).
}

@MaterialProperty stress young; 
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");


@LocalVariable stress λ,μ;
/* Initialize Lame coefficients */
@InitLocalVariables{
  using namespace tfel::material::lame;
  λ = computeLambda(young,nu);
  μ = computeMu(young,nu);
} // end of @InitLocalVars

@PredictionOperator {
  static_cast<void>(smt);
  Dt = λ ⋅ (I₂ ⊗ I₂) + 2 ⋅ μ ⋅ I₄;
}

@Integrator{
  const auto ε = εᵗᵒ + Δεᵗᵒ;
  σ = λ ⋅ trace(ε) ⋅ I₂ + 2 ⋅ μ ⋅ ε;
}

@TangentOperator {
  static_cast<void>(smt);
  Dt = λ ⋅ (I₂ ⊗ I₂) + 2 ⋅ μ ⋅ I₄;
}
